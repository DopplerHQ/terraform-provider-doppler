---
page_title: "doppler_integration_gcp_cloudsql_mysql Resource - terraform-provider-doppler"
subcategory: "Integrations"
description: |-
	Manage a GCP CloudSQL MySQL Doppler integration.
---

# doppler_integration_gcp_cloudsql_mysql (Resource)

Manage a GCP CloudSQL MySQL Doppler integration.

## Example Usage

```terraform
# The GCP key that grants access to our CloudSQL instance. For the purposes of this example, this is provided directly.
variable "gcp_key" {
  type    = string
  default = <<-EOT
  {
        "type": "service_account",
        "project_id": "xxxxxxxxxxxxxxxxx",
        "private_key_id": "xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx",
        "private_key": "-----BEGIN PRIVATE KEY-----...-----END PRIVATE KEY-----",
        "client_email": "xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx",
        "client_id": "xxxxxxxxxxxxxxxxxxxxx"
  }
  EOT
  sensitive = true
}

# The initial password for the first db user. This will be rotated, so we provide a default and ignore changes below.
# Provide this with -var-file: https://developer.hashicorp.com/terraform/language/values/variables#variable-definitions-tfvars-files 
variable "db_password_1" {
  type = string
  default = ""
  # Consider using ephemeral instead if your client supports it: https://developer.hashicorp.com/terraform/language/values/variables#exclude-values-from-state
  sensitive = true
}
variable "db_password_2" {
  type = string
  default = ""
  sensitive = true
}

resource "doppler_integration_gcp_cloudsql_mysql" "i_gcp_mysql" {
  name    = "TF GCP MySQL"
  gcp_key = var.gcp_key
}

resource "doppler_rotated_secret_gcp_cloudsql" "rs_gcp_mysql" {
  integration         = doppler_integration_gcp_cloudsql_mysql.i_gcp_mysql.id
  project             = "backend"
  config              = "dev"
  name                = "DB"
  rotation_period_sec = 2592000
  database_instance   = "xxxxxxxxxxxx"
  credentials {
    username       = "xxxxxxxxxxxx"
    user_host_name = "%"
    password       = var.db_password_1
  }
  credentials {
    username       = "xxxxxxxxxxxx"
    user_host_name = "%"
    password       = var.db_password_2
  }
  lifecycle {
    # The credentials are rotated regularly by Doppler, and cannot be updated via TF after initialization, so skip checking the credentials against state.
    ignore_changes = [credentials]
  }
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `gcp_key` (String, Sensitive) The GCP service account key
- `name` (String) The name of the integration

### Read-Only

- `id` (String) The ID of this resource.
